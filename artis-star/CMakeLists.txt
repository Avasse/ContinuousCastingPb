CMAKE_MINIMUM_REQUIRED(VERSION 2.6)
PROJECT(ARTIS CXX C)
ENABLE_TESTING()

SET(ARTIS_MAJOR 1)
SET(ARTIS_MINOR 0)
SET(ARTIS_PATCH 0)
SET(ARTIS_EXTRA "")
SET(ARTIS_VERSION "${ARTIS_MAJOR}.${ARTIS_MINOR}.${ARTIS_PATCH}")
SET(ARTIS_VERSION_SHORT "${ARTIS_MAJOR}.${ARTIS_MINOR}")
SET(ARTIS_NAME "artis-star")
SET(ARTIS_NAME_VERSION "${ARTIS_NAME}-${ARTIS_VERSION}")
SET(ARTIS_LOCALE_NAME "${ARTIS_NAME}-${ARTIS_MAJOR}-${ARTIS_MINOR}")
IF ("${ARTIS_EXTRA}" STREQUAL "")
  SET(ARTIS_COMPLETE "${ARTIS_MAJOR}.${ARTIS_MINOR}.${ARTIS_PATCH}")
ELSE ("${ARTIS_EXTRA}" STREQUAL "")
  SET(ARTIS_COMPLETE "${ARTIS_MAJOR}.${ARTIS_MINOR}.${ARTIS_PATCH}-${ARTIS_EXTRA}")
ENDIF ("${ARTIS_EXTRA}" STREQUAL "")
SET(ARTIS_NAME_COMPLETE "${ARTIS_NAME}-${ARTIS_COMPLETE}")

#
 # Installation variables
#

SET(ARTIS_LIBRARY_DIRS "lib/artis-star-${ARTIS_VERSION_SHORT}")
SET(ARTIS_INCLUDE_DIRS "include/artis-star-${ARTIS_VERSION_SHORT}/artis-star")
SET(ARTIS_INCLUDE_DIR "include/artis-star-${ARTIS_VERSION_SHORT}")
SET(ARTIS_MANPAGE_PATH "share/man/man1")
SET(ARTIS_SHARE_DIRS "share/artis-star-${ARTIS_VERSION_SHORT}")

#
 # Append the library version information to the library target
 # properties.
#

SET (ARTIS_LIBRARY_PROPERTIES VERSION 0)
SET (ARTIS_APP_PROPERTIES SOVERSION "${ARTIS_MAJOR}.${ARTIS_MINOR}"
  VERSION "${ARTIS_MAJOR}.${ARTIS_MINOR}")

#
 # CPack configuration
#

INCLUDE(CMakeCPack.cmake)

#
 # Debug mode
#

IF (CMAKE_COMPILER_IS_GNUCC AND CMAKE_COMPILER_IS_GNUCXX)
  SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Wall -Wextra")
  SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -std=c++11 -pthread")
  IF (UNIX)
    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -pedantic")
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -pedantic")
  ENDIF (UNIX)
ENDIF (CMAKE_COMPILER_IS_GNUCC AND CMAKE_COMPILER_IS_GNUCXX)

if (CMAKE_BUILD_TYPE STREQUAL "")
  SET(CMAKE_BUILD_TYPE "RelWithDebInfo" CACHE STRING "Choose the type of build" FORCE)
endif ()

#
 # Modules
#

INCLUDE(CheckIncludeFileCXX)
INCLUDE(CheckIncludeFile)
INCLUDE(CheckLibraryExists)
INCLUDE(CMakeDetermineCCompiler)

#
 # Test the libboost header and libboost-text library.
#

OPTION(WITH_TEST "use the libboost test unit library [default: on]" ON)
SET(Boost_USE_MULTITHREADED ON)
SET(Boost_USE_STATIC_LIBS OFF)
SET(Boost_NO_SYSTEM_PATHS OFF)
SET(Boost_USE_STATIC_RUNTIME ON)
IF (WIN32)
  SET(Boost_THREADAPI "win32")
ENDIF (WIN32)
SET(Boost_DETAILED_FAILURE_MSG FALSE)
SET(Boost_DEBUG FALSE)

FIND_PACKAGE(Boost COMPONENTS graph mpi timer serialization system)
IF (NOT Boost_GRAPH_FOUND)
  MESSAGE(FATAL_ERROR "The boost graph library is required")
ENDIF (NOT Boost_GRAPH_FOUND)
IF (Boost_MINOR_VERSION AND ${Boost_MINOR_VERSION} LESS "35")
  SET(Boost_SYSTEM_LIBRARY "")
ENDIF (Boost_MINOR_VERSION AND ${Boost_MINOR_VERSION} LESS "35")
IF (Boost_MINOR_VERSION AND ${Boost_MINOR_VERSION} LESS "41")
  SET(ARTIS_HAVE_BOOST_SPIRIT2 0 CACHE INTERNAL "" FORCE)
ELSE (Boost_MINOR_VERSION AND ${Boost_MINOR_VERSION} LESS "41")
  SET(ARTIS_HAVE_BOOST_SPIRIT2 1 CACHE INTERNAL "" FORCE)
ENDIF (Boost_MINOR_VERSION AND ${Boost_MINOR_VERSION} LESS "41")

#
 # Generate the config.h
#

CHECK_INCLUDE_FILE_CXX(cxxabi.h ARTIS_HAVE_GCC_ABI_DEMANGLE)
CHECK_INCLUDE_FILE_CXX(execinfo.h ARTIS_HAVE_EXECINFO)
CHECK_INCLUDE_FILE_CXX(signal.h ARTIS_HAVE_SIGNAL)
INCLUDE_DIRECTORIES(${ARTIS_BINARY_DIR})

#
 # Generate the vle.pc pkg-config file.
 # Build some variable to build a correct Win32 pkg config file with:
 # prefix=/target to detect the DIRNAME
 # cflag=-Iboost_1_34_1 for the directory
#

SET(ARTIS_PKGCONFIG_PREFIXPATH "${CMAKE_INSTALL_PREFIX}")
SET(ARTIS_PKGCONFIG_BOOSTINCLUDE_DIRS "${Boost_INCLUDE_DIRS}")
SET(ARTIS_PKGCONFIG_LIBDIR "lib")

CONFIGURE_FILE(${PROJECT_SOURCE_DIR}/src/artis-star.pc.in
  ${ARTIS_BINARY_DIR}/artis-star-${ARTIS_VERSION_SHORT}.pc @ONLY)
INSTALL(FILES ${ARTIS_BINARY_DIR}/artis-star-${ARTIS_VERSION_SHORT}.pc
  DESTINATION lib/pkgconfig)

#
 # Install the README
#

IF(UNIX)
  INSTALL(FILES "README" DESTINATION "${ARTIS_SHARE_DIRS}")
  INSTALL(FILES "COPYING" DESTINATION "${ARTIS_SHARE_DIRS}")
ELSE(UNIX)
  INSTALL(FILES "README" DESTINATION "${ARTIS_SHARE_DIRS}" RENAME "Readme.txt")
  INSTALL(FILES "COPYING" DESTINATION "${ARTIS_SHARE_DIRS}" RENAME "CopyRight.txt")
ENDIF(UNIX)

#
 # Browse the src subdirectory
#

ADD_SUBDIRECTORY(src)

#
 # Status
#

MESSAGE(STATUS "- - - -")
MESSAGE(STATUS "${ARTIS_NAME_COMPLETE} configured successfully")
MESSAGE(STATUS "Using ${CMAKE_INSTALL_PREFIX} for installation")
MESSAGE(STATUS "Build type ${CMAKE_BUILD_TYPE}")
IF (ARTIS_HAVE_GCC_ABI_DEMANGLE)
  MESSAGE(STATUS "Build with GCC ABI Demangle...: yes")
ENDIF (ARTIS_HAVE_GCC_ABI_DEMANGLE)
IF (ARTIS_HAVE_EXECINFO)
  MESSAGE(STATUS "Build with execinfo.h.........: yes")
ENDIF (ARTIS_HAVE_EXECINFO)
IF (Boost_FOUND)
  MESSAGE(STATUS "Build with boost..............: yes")
ENDIF (Boost_FOUND)
MESSAGE(STATUS "- - - -")

# vim:tw=0:ts=8:tw=0:sw=2:sts=2
